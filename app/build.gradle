plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk = 36

    defaultConfig {
        applicationId "net.blumia.pineapple.lockscreen"
        minSdk 28
        targetSdk 36
        versionCode 18
        versionName "2.1.2"

        manifestPlaceholders = [
            launcherIcon: "@mipmap/ic_launcher_green_lock",
            launcherIconRound: "@mipmap/ic_launcher_green_round"
        ]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    flavorDimensions = ["mode"]
    productFlavors {
        register("free") {
            dimension = "mode"

            resValue "bool", "supportQuickSettingsTile", "false"
            resValue "bool", "supportAppWidget", "false"
            buildConfigField "boolean", "PROMINENT_DISCLOSURE_REQUIRED", "true"
            buildConfigField "boolean", "USE_DEPRECATED_SHORTCUT_METHOD", "true"
            buildConfigField "boolean", "PROMOTE_PLUS_VERSION", "true"
            buildConfigField "String", "STORE_LINK", "\"https://play.google.com/store/apps/details?id=%s\""
        }
        register("plus") {
            dimension = "mode"
            applicationIdSuffix = ".plus"
            versionNameSuffix = "-plus"

            manifestPlaceholders = [
                launcherIcon: "@mipmap/ic_launcher_blue_lock",
                launcherIconRound: "@mipmap/ic_launcher_blue_round"
            ]
            resValue "bool", "supportQuickSettingsTile", "true"
            resValue "bool", "supportAppWidget", "true"
            buildConfigField "boolean", "PROMINENT_DISCLOSURE_REQUIRED", "true"
            buildConfigField "boolean", "USE_DEPRECATED_SHORTCUT_METHOD", "true"
            buildConfigField "boolean", "PROMOTE_PLUS_VERSION", "false"
            buildConfigField "String", "STORE_LINK", "\"https://play.google.com/store/apps/details?id=%s\""
        }
        register("oss") {
            dimension = "mode"
            applicationIdSuffix = ".oss"
            versionNameSuffix = "-oss"

            resValue "bool", "supportQuickSettingsTile", "true"
            resValue "bool", "supportAppWidget", "true"
            buildConfigField "boolean", "PROMINENT_DISCLOSURE_REQUIRED", "false"
            buildConfigField "boolean", "USE_DEPRECATED_SHORTCUT_METHOD", "false"
            buildConfigField "boolean", "PROMOTE_PLUS_VERSION", "true"
            // Change this to other FOSS store link when we have one.
            buildConfigField "String", "STORE_LINK", "\"https://f-droid.org/packages/%s/\""
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        // Compose compiler and kotlin version table: https://developer.android.com/jetpack/androidx/releases/compose-kotlin
        kotlinCompilerExtensionVersion "1.4.5"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    namespace 'net.blumia.pineapple.lockscreen'
}

dependencies {
    // Import the Compose BOM
    // BOM to library version mapping: https://developer.android.com/jetpack/compose/bom/bom-mapping
    def composeBOM = platform('androidx.compose:compose-bom:2025.12.01')
    implementation composeBOM
    androidTestImplementation composeBOM
    // Declare dependencies for the desired Compose libraries without versions
    implementation 'androidx.compose.foundation:foundation'

    implementation 'androidx.core:core-ktx:1.17.0'
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.material3:material3"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.material:material-icons-core"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.10.0'
    implementation 'androidx.activity:activity-compose:1.12.2'
    implementation "androidx.glance:glance-appwidget:1.1.1"
    implementation "androidx.datastore:datastore-preferences:1.2.0"
    def nav_compose_version = '2.9.0'
    implementation "androidx.navigation:navigation-compose:$nav_compose_version"
    def latestAppCompatVersion = "1.7.1"
    implementation "androidx.appcompat:appcompat:$latestAppCompatVersion"
    implementation "androidx.appcompat:appcompat-resources:$latestAppCompatVersion"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
    debugImplementation "androidx.compose.ui:ui-tooling"
}